plugins {
    id 'java'
}

group 'com.wynprice'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    compile 'io.netty:netty-all:4.1.39.Final'

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'
}

task clientJar(type: Jar) {
    classifier = "client"
    manifest.attributes (
            "Main-Class": "com.wynprice.cafedafydd.client.CafeDafyddMain"
    )
    with jar
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude "com/wynprice/cafedafydd/server/**"
}

task serverJar(type: Jar) {
    classifier = "server"
    manifest.attributes (
            "Main-Class": "com.wynprice.cafedafydd.server.CafeDafyddServerMain"
    )
    with jar
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude "com/wynprice/cafedafydd/client/**"

    sourceSets.main.output.forEach { }
}

artifacts {
    archives clientJar
    archives serverJar
}

boolean checkImport(String base, String... blacklisted) {
    boolean hit = false
    fileTree("src/main/java/com/wynprice/cafedafydd/$base").filter { it.isFile() }.forEach {
        for(String s : blacklisted) {
            s = "com.wynprice.cafedafydd.$s"
            def lines = it.readLines()
            for(int i = 0; i < lines.size(); i++) {
                if(lines.get(i).contains(s)) {
                    println("Found blacklisted import $s in $it.name at line $i (${lines.get(i)})")
                    hit = true
                }
            }
        }
    }
    hit
}

task checkAllImports () {
    if(checkImport("common", "client", "server") | checkImport("client", "server") | checkImport("server", "client")) {
        throw new GradleException("Blacklist Import Found. See above for details")
    }
}
